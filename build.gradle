buildscript {
    ext {
        kotlinVersion = '1.2.0'
        springBootVersion = '1.5.7.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'


group = 'com.piotrwalkusz.lebrb'
version = '0.1.0'


sourceSets {
    integration {
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

configurations {
    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime
}



repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis'
    compile group: 'org.springframework.session', name: 'spring-session-data-redis'
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    compile 'com.auth0:java-jwt:3.3.0'
    compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity4', version: '2.1.3.RELEASE'
    runtime('org.springframework.boot:spring-boot-devtools')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    integrationCompile group: 'info.cukes', name: 'cucumber-spring', version: '1.2.5'
    integrationCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.5'
}

idea {
    module {
        scopes.TEST.plus += [configurations.integrationCompile]
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}


task integration {
    dependsOn integrationClasses
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.integrationRuntime + sourceSets.integration.output
            args = ['--plugin', 'pretty', 'src/integration/resources']
        }
    }
}
